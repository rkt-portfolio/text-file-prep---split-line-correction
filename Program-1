	* THE FOLLOWING CODE CORRECTS THE SPLIT LINES AND APPENDS THEM AT THE END OF THE GOOD DATA;

%MACRO READIN(IN,OUT,DATASTART);
DATA &OUT.;
  	INFILE "&PATH.\RAWDATA\&IN."  TRUNCOVER LRECL=32767 FIRSTOBS=1;
	INPUT TOTAL_TEXT $1000.;
	TOTAL_TEXT = COMPRESS(TOTAL_TEXT,'"');
	TOTAL_TEXT = STRIP(TOTAL_TEXT);
	COUNT_DELIM = COUNT(TOTAL_TEXT,'|');
	LINE_CNT = _N_;
RUN;
%MEND;
%READIN (DT_JOURNALLISTING_07_2013.txt, TEMP_JE01);
%READIN (DT_JOURNALLISTING_08_2013.txt, TEMP_JE02);
%READIN (DT_JOURNALLISTING_09_2013.txt, TEMP_JE03);
%READIN (DT_JOURNALLISTING_10_2013.txt, TEMP_JE04);
%READIN (DT_JOURNALLISTING_11_2013.txt, TEMP_JE05);
%READIN (DT_JOURNALLISTING_12_2013.txt, TEMP_JE06);

/*SEGREGATING THE RECORDS WITH LINES WITH < 51 DELIMITERS */

DATA TEMP_JE;
SET TEMP_JE01 TEMP_JE02 TEMP_JE03 TEMP_JE04 TEMP_JE05 TEMP_JE06;
RUN;

DATA GOOD BAD1 BAD2 ;
	SET TEMP_JE;
	IF COUNT_DELIM = 51 THEN OUTPUT GOOD;
	ELSE IF COUNT_DELIM LT 51 THEN OUTPUT BAD1;
	ELSE OUTPUT BAD2;
RUN;

/*FIXING THE LINES WITH  < 51 DELIMITERS & >51 DELIMITERS */

DATA BAD1_REPAIR;
	LENGTH TOTAL_TEXT1 $1000.;
SET BAD1;
	RETAIN TOTAL_TEXT1 COUNT1;
	TOTAL_TEXT1 = COMPRESS(TRIM(TOTAL_TEXT1)||TRIM(TOTAL_TEXT),'"');
	COUNT1 = SUM(COUNT1,COUNT_DELIM);
	IF (COUNT1 EQ 51) THEN DO;
		OUTPUT;
		TOTAL_TEXT1 = "";
		COUNT1 = 0;
	END;
		DROP TOTAL_TEXT;
		RENAME TOTAL_TEXT1 = TOTAL_TEXT;
RUN;

DATA BAD1_REPAIR_1(KEEP = TOTAL_TEXT);
	SET BAD1_REPAIR;
	TOTAL_TEXT =COMPRESS(TOTAL_TEXT,'09'X); /* COMPRESSING TAB CHARACTERS */
RUN;

PROC SUMMARY DATA=BAD2 NWAY MISSING;
CLASS COUNT_DELIM;
var LINE_CNT;
OUTPUT OUT=BAD2_DELIM_SUMM;
RUN;

DATA BAD2_REPAIR_2(KEEP = TOTAL_TEXT_NEW RENAME = (TOTAL_TEXT_NEW = TOTAL_TEXT));
	SET BAD2;
	REV_TOTAL_TEXT = REVERSE(TOTAL_TEXT);

/*	THE 17TH PIPE FROM LAST IS AN EXTRA PIPE WHICH NEEDS TO BE REMOVED*/
	
	B = INDEX(REV_TOTAL_TEXT,'|');
	DO I=2 TO 17 BY 1;
	B = FIND(REV_TOTAL_TEXT,'|',B+1);
	END;
	
	SUBSTR(REV_TOTAL_TEXT,B,1)= '';
	TOTAL_TEXT_NEW =REVERSE(COMPRESS(REV_TOTAL_TEXT,'"')); 
RUN;

DATA JE_REPAIRED;
	SET BAD2_REPAIR_2(KEEP =TOTAL_TEXT) GOOD(KEEP = TOTAL_TEXT) BAD1_REPAIR_1(KEEP = TOTAL_TEXT) ;
RUN;

/*EXPORTING THE FINAL REPAIRED FILE INTO PIPE DELIMITED TXT.*/

PROC EXPORT DATA= WORK.JE_REPAIRED 
            OUTFILE= "&PATH.\RAWDATA\JE_REPAIRED.txt" 
            DBMS=DLM REPLACE;
     DELIMITER='5E'x; 
RUN;

